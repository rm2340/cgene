module Gillespie;
export Substance, name;
export Substrate;
import * from ABS.StdLib;
import Reaction from Reaction;
import * from MathAux;

// A Substance
data Substance = Substance(String name);

/* This is the environment 
 * - it contains substances
 * - that react with each other according to the given reactions
 *
 */
interface Substrate {
    Bool testSubstances(Set<Substance> reactants);
    Bool consumeSubstances(Map<Substance, Int> consumption);
    Unit produceSubstances(Map<Substance, Int> production);
    Int reactionFactor(Map<Substance, Int> consumption);
    Unit run();
    Unit logRate(String desc, Rat rate);
}

class Substrate(Map<Substance, Int> substances, Set<Reaction> reactions) implements Substrate {
    Map<Reaction,Rat> reactionRates = map[];
    
    Unit run() {
        this.logInit();
        // start all reactions
        Int n = 1;
        while (hasNext(reactions)) {
            Reaction r = take(reactions);
            reactions = remove(reactions, r);
            
            String name = await r!toString();
            String id = "reaction" + toString(n);
            await r!setID(id);
            println(id + " " + name);
            n = n + 1;

            r!start(this);
        }
    }
    
    /*
     * Sanity check: do all reactants exist in the substrate? (they should)
     * TODO: raise exception if not
     */
    Bool testSubstances(Set<Substance> reactants) {
        Bool success = True;
        Set<Substance> availableSubstances = keys(substances);

        // Test if every reactant exists
        Bool continue = True;
        while (hasNext(reactants) && continue) {
            Substance s = take(reactants);
            reactants = remove(reactants, s);
            if (! contains(availableSubstances, s)) {
                continue = False;
                success = False;
            }
        }
        return success;
    }
    
    /* Returns True if reactants exist in sufficient qunatity for the reaction to occur
     */
    Bool consumeSubstances(Map<Substance, Int> consumption) {
        Bool success = False;
        Set<Substance> availableSubstances = keys(substances);
	    Set<Substance> requiredSubstances = keys(consumption);

        // Test if every required substance exists in the required quantity
	    Bool continue = True;
	    while (hasNext(requiredSubstances) && continue) {
	        Substance s = take(requiredSubstances);
	        requiredSubstances = remove(requiredSubstances, s);
	        Int q = lookupUnsafe(consumption, s);
            Int availableQuantity = lookupUnsafe(substances, s);
            if (q > availableQuantity) {
                continue = False;
            }
	    }
	    
	    if (continue) {
		    // Consume the substances
		    Set<Substance> consumedSubstances = keys(consumption);
		    while (hasNext(consumedSubstances)) {
		        Substance s = take(consumedSubstances);
		        consumedSubstances = remove(consumedSubstances, s);
		        Int q = lookupUnsafe(consumption, s);
		        Int availableQuantity = lookupUnsafe(substances, s);
		        substances = put(substances, s, availableQuantity - q);
		    }
		    success = True;
		} else {
		    success = False;
		}
		return success;
    }

    Unit produceSubstances(Map<Substance, Int> production) {
        Set<Substance> producedSubstances = keys(production);
        while (hasNext(producedSubstances)) {
            Substance s = take(producedSubstances);
            producedSubstances = remove(producedSubstances, s);
            Int q = lookupUnsafe(production, s);
            Int availableQuantity = lookupUnsafe(substances, s);
            substances = put(substances, s, availableQuantity + q);
        }
        this.logg();
    }
    
    /* The reaction factor is a function of the quantity of available molecules of all reactants.
     * Default: 1
     * If zero (at least one reactant has zero molecules), revert to default
     *
     * TODO: Consider the quantity of required molecules (consumption)?
     */
    Int reactionFactor(Map<Substance, Int> consumption) {
        Set<Substance> availableSubstances = keys(substances);
        Set<Substance> requiredSubstances = keys(consumption);
        
        Int reactionFactor = 1;   
        while (hasNext(requiredSubstances)) {
            Substance s = take(requiredSubstances);
            requiredSubstances = remove(requiredSubstances, s);
            Int availableQuantity = lookupUnsafe(substances, s);
            reactionFactor = reactionFactor * availableQuantity;
        }
        // If at least one reactant has quantity zero, assume the default factor of 1 (avoid division by zero later).
        if (reactionFactor == 0) {
            reactionFactor = 1;
        }
        assert reactionFactor >= 1;
        return reactionFactor;
    }
    
    
    
    /*
     * Logging/plotting output
     */
    Unit logInit() {
        Set<Substance> availableSubstances = keys(substances);
        print("num den");
        while (hasNext(availableSubstances)) {
            Substance s = take(availableSubstances);
            availableSubstances = remove(availableSubstances, s);
            print("    " + name(s));
        }
        println("");
    }
    
    Unit logg() {
        Set<Substance> availableSubstances = keys(substances);
        print(toString(numerator(timeValue(now()))) + " " + toString(denominator(timeValue(now()))));
        while (hasNext(availableSubstances)) {
            Substance s = take(availableSubstances);
            availableSubstances = remove(availableSubstances, s);
            print("    " + toString(lookupUnsafe(substances, s)));
        }
        println("");
    }
    
    Unit logRate(String id, Rat rate) {
        print(toString(numerator(timeValue(now()))) + " " + toString(denominator(timeValue(now()))));
        print("   " + id);
        println("   " + toString(numerator(rate)) + " " + toString(denominator(rate)));    
    }
}
