module Mrna;
export Mrna, Nucleotide, A, C, G, U, nuc, stop;
import * from Ribosome;

// adenine, cytosine, guanine, uracil
data Nucleotide = A | C | G | U;

def Nucleotide nuc() =
    case random(4) {
        0 => A;
        1 => C;
        2 => G;
        3 => U;
    };


/* // Would be nice if this would work...
def Bool stop(List<Nucleotide> codon) =
    case codon {
        list[U, A, A] => true;
        list[U, A, G] => true;
        list[U, G, A] => true;
        _ => false;
    };
*/
def Bool stop(List<Nucleotide> codon) =
    case codon {
        Cons(U, Cons(A, Cons(A, Nil))) => True;
        Cons(U, Cons(A, Cons(G, Nil))) => True;
        Cons(U, Cons(G, Cons(A, Nil))) => True;
        _ => False;
    };


interface Mrna {
    Int attach(Ribosome rib);
    Unit detach(Ribosome rib, Int pos);
    Unit advance(Ribosome rib, Int pos);
    List<Nucleotide> readCodon(Int pos);
    Nucleotide readNucleotide(Int pos);
    String toString();
}

class Mrna(List<Nucleotide> mrna) implements Mrna {

    // This map remembers the positions of ribosomes in the mRNA strand
    // Only one ribosome can be at a certain position
    Map<Int, Maybe<Ribosome>> occupancy = map[];

    // init block (constructor)
    {
        Int i = 0;
        while(i < length(mrna)) {
            occupancy = put(occupancy, i, Nothing);
            i = i + 1;
        }
    }


    /* Attach ribosome at random position in the strand
     * Return position where ribosome attached
     * or -1 if it failed to attach
     */
    Int attach(Ribosome rib) {
        Int success = -1;
        Int pos = random(length(mrna));
        if (lookupUnsafe(occupancy, pos) == Nothing) {
            occupancy = put(occupancy, pos, Just(rib));
            success = pos;
            println("Ribosome attached to mRNA at pos " + toString(pos));
        }
        return success;
    }

    Unit detach(Ribosome rib, Int pos) {
        // assume rib is indeed at pos
        assert lookupUnsafe(occupancy, pos) == Just(rib);

        // clear pos
        occupancy = put(occupancy, pos, Nothing);
        //println("Ribosome detached from mRNA");

        // decoding the entire mrna takes 1, regadless of how long it is
        // TODO let decoding each codon have certain duration
        duration(1,1);
    }

    // Move the Ribosome rib from position pos to pos+1 (one nucleotide further)
    Unit advance(Ribosome rib, Int pos) {
        //wait until next position is free
        await(lookupUnsafe(occupancy, pos+1) == Nothing);

        // clear current pos & move rib to next pos
        occupancy = put(put(occupancy, pos, Nothing), pos+1, Just(rib));

        //String s = this.occupancyToString();
        //println("Occupancy: " + s);
    }

    // Read codon at position pos
    List<Nucleotide> readCodon(Int pos) {
        List<Nucleotide> codon = list[nth(mrna, pos), nth(mrna, pos+1), nth(mrna, pos+2)];
        return codon;
    }

    Nucleotide readNucleotide(Int pos) {
        return nth(mrna, pos);
    }


    String toString() {
        return toString(mrna);
    }
    String occupancyToString() {
        String res = "";
        Int i = 0;
        while(i < size(keys(occupancy))) {
            if (lookupUnsafe(occupancy, i) == Nothing)
                res = res + "- ";
            else
                res = res + "* ";
            i = i + 1;
        }
        return res;
    }

}
