module Rna;
export Rna, Folding, pairs;
export RnaGetters, RnaSecondaryStructure;
import * from Dna;
import * from Cell;


data Folding = Folding(Map<Int,Int> pairs);

interface Rna {
    Unit fold(Folding f);
    Unit insert(Rna newrna, Int pos);
    String getID();
    Cell getCell();
    Unit setCell(Cell cell);
    List<Nucleotide> getCode();
    Folding getFolding();
    Int secondaryStructureStrength();
    Int secondaryStructureStrengthAtPos(Int pos1);
    String toDotBracket();
}

class Rna(Gene gene) implements Rna {
    String id = id(gene);
    Cell cell; // set it when adding this rna to a cell
    List<Nucleotide> code = code(gene);
    Folding folding = Folding(map[]); // initially no folding

    uses RnaGetters;
    uses RnaSecondaryStructure;
}


trait RnaGetters = {
    Cell getCell() {
        return cell;
    }
    Unit setCell(Cell cell) {
        this.cell = cell;
    }
    String getID() {
        return id;
    }
    List<Nucleotide> getCode() {
        return code;
    }
    Folding getFolding() {
        return folding;
    }
}

trait RnaSecondaryStructure = {

    Unit fold(Folding f) {
        folding = f;
    }

    Unit insert(Rna insertedRna, [Final] Int pos) {
        List<Nucleotide> oldcode = code;
        List<Nucleotide> newcode = list[];
        Int lengthOldcode = length(oldcode);
        try assert pos < lengthOldcode;
        catch AssertionFailException => println("Cannot insert RNA at pos " + toString(pos));
        List<Nucleotide> inscode = await insertedRna!getCode();
        Int len = length(inscode);

        /****** 1. Insert Code at pos ******/
        // copy first <pos> elements
        // copy inserted code
        // copy rest of elements
        Int i = 0;
        while (i < pos) {
            newcode = appendright(newcode, head(oldcode));
            oldcode = tail(oldcode);
            i = i + 1;
        }
        newcode = concatenate(newcode, inscode);
        newcode = concatenate(newcode, oldcode);
        code = newcode;
        assert length(newcode) == lengthOldcode + length(inscode);

        /****** 2. Adjust Folding ******/
        // inserted molecule:
        Folding fi = await insertedRna!getFolding();
        Map<Int, Int> insmap = pairs(fi);
        Map<Int, Int> newmap = map[];
        List<Int> keys = elements(keys(insmap));

        while (! isEmpty(keys)) {
            Int k = head(keys);
            Int v = lookupUnsafe(insmap, k);
            assert k < v+1;
            assert lookup(newmap, k+pos) == Nothing;
            assert lookup(newmap, v+pos) == Nothing;
            newmap = put(newmap, k+pos, v+pos);
            keys = tail(keys);
        }
        assert length(entries(insmap)) == length(entries(newmap));

        // this molecule:
        Map<Int, Int> oldmap = pairs(folding);
        keys = elements(keys(oldmap));
        while(! isEmpty(keys)) {
            Int k = head(keys);
            Int v = lookupUnsafe(oldmap, k);
            assert k < v+1;
            if (v >= pos) {
                v = v+len;
                if (k >= pos)
                    k = k+len;
            }
            //println("put " + toString(k) + "->" + toString(v));
            assert lookup(newmap, k) == Nothing;
            assert lookup(newmap, v) == Nothing;
            newmap = put(newmap, k, v);
            keys = tail(keys);
        }
        //println(toString(length(entries(newmap))) + ": " + toString(newmap));
        // done
        folding = Folding(newmap);

        this.checkFolding();
    }

    /* Calculate an overall value for the secondary structure strength
     * based on total nr of hydrogen bonds

    Int secondaryStructureStrength() {
        Int bond = 0;
        Int pos = 0;
        while (pos < length(code)) {
            Int b = this.hydrogenBondsAt(pos);
            bond = bond + b;
            pos = pos + 1;
        }
        return bond;
    }
    */

    /* Calculate an overall value for the secondary structure strength
     * based on total nr of hydrogen bonds in the **stems only**
     */
    Int secondaryStructureStrength() {
        Int strength = 0;
        Int pos1 = 0;
        while (pos1 < length(code)) {
            Maybe<Int> pos2 = lookup(pairs(folding), pos1);
            if (isJust(pos2)) {
                //println("Stem at " + toString(pos1) + "--" + toString(fromJust(pos2)));
                Int st = this.secondaryStructureStrengthAtPos(pos1);
                strength = strength + st;
                //println("Strength " + toString(st));
                // next search will start at pos2+1
                pos1 = fromJust(pos2) + 1;
            } else {
                pos1 = pos1 + 1;
            }
        }
        return strength;
    }

    /* Calculate the number of secondary structures (aptamers),
       which is equal to the number of stems
     */
    Int secondaryStructureStems() {
        Int stems = 0;
        Int pos1 = 0;
        while (pos1 < length(code)) {
            Maybe<Int> pos2 = lookup(pairs(folding), pos1);
            if (isJust(pos2)) {
                //println("Stem at " + toString(pos1) + "--" + toString(fromJust(pos2)));
                stems = stems + 1;
                // next search will start at pos2+1
                pos1 = fromJust(pos2) + 1;
            } else {
                pos1 = pos1 + 1;
            }
        }
        return stems;
    }



    /* Return strength of secondary structure at pos1
     * based on nr of consecutive hydrogen bonds at pos1 and following
     */
    Int secondaryStructureStrengthAtPos(Int pos) {
        Int bond = 0;
        Int b = -1;
        // FIXME avoid reading past the end of molecule
        while (b != 0) {
            b = this.hydrogenBondsAt(pos);
            bond = bond + b;
            pos = pos + 1;
        }
        return bond;
    }

    /*
     * Return hydrogen bonds at given position
     * - either 0, 2, or 3
     *   A-U -> 2
     *   G-C -> 3
     */
    Int hydrogenBondsAt(Int pos1) {
        Int bonds = 0;
        Maybe<Int> pos2 = lookup(pairs(folding), pos1);
        if (isJust(pos2)) {
            Nucleotide n1 = nth(code, pos1);
            Nucleotide n2 = nth(code, fromJust(pos2));
            if (n1 == A) {
                assert n2 == U;
                bonds = 2;
            } else if (n1 == C) {
                assert n2 == G;
                bonds = 3;
            } else if (n1 == G) {
                assert n2 == C;
                bonds = 3;
            } else if (n1 == U) {
                assert n2 == A;
                bonds = 2;
            }
        }
        return bonds;
    }

    // check whether folding is correct, i.e. only G-C and A-U pairs
    Unit checkFolding() {
        List<Pair<Int,Int>> pairs = entries(pairs(folding));
        while(!isEmpty(pairs)) {
            Pair<Int,Int> p = head(pairs);
            Nucleotide n1 = nth(code, fst(p));
            Nucleotide n2 = nth(code, snd(p));
            try assert validBasePair(n1,n2);
            catch AssertionFailException => println("Invalid base pair: " + toString(n1) + "-" + toString(n2));
            pairs = tail(pairs);
        }
    }

    String toDotBracket() {
        String primary = "";
        String secondary = "";
        Set<Int> keys = keys(pairs(folding));
        Set<Int> vals = set[];

        Int pos = 0;
        String char = "";
        foreach (n in code) {
            primary = primary + toString(n);
            if (contains(keys, pos)) {
                char = "(";
                vals = insertElement(vals, lookupUnsafe(pairs(folding), pos));
                //println(toString(pos) + "--" + toString(lookupUnsafe(pairs(folding), pos)));
            } else if (contains(vals, pos))
                char = ")";
            else
                char = ".";
            secondary = secondary + char;
            pos = pos + 1;
        }
        return primary + "\n" + secondary;
    }

}
