module Cell;
export Cell;
import * from Transcription;
import * from Mrna;
import * from Ribosome;
import * from Protein;

interface Cell {
    Unit addMRNA(Mrna mrna);
    Unit addRibosome(Ribosome ribo);
    Unit addProtein(Protein prot);
    Mrna obtainMrna();
}

class Cell implements Cell {
    List<Mrna> mrnaStrands = list[];
    Set<Ribosome> ribosomes = set[];
    Set<Protein> proteins = set[];

    Unit addMRNA(Mrna mrna) {
        mrnaStrands = appendright(mrnaStrands, mrna);
    }

    Unit addRibosome(Ribosome ribo) {
        ribosomes = insertElement(ribosomes, ribo);
    }

    Unit addProtein(Protein prot) {
        proteins = insertElement(proteins, prot);
    }

    // Randomly return one of the mRNA strands for decoding
    Mrna obtainMrna() {
        Int n = random(length(mrnaStrands));
        Mrna mrna = nth(mrnaStrands, n);
        String s = await mrna!toString();
        println("Cell: offering mRNA: " + s);
        return mrna;
    }
}


/* Main block */

{
    Cell cell = new Cell();

    // populate cell with mRNA strands
    Transcription ts = new Transcription();

    Int i = 0;
    Int population = 1; // how many objects to generate


    while (i < population) {
        Mrna mrna = ts.genMRNA();
        cell.addMRNA(mrna);
        i = i + 1;
    }

    // populate cell with ribosomes
    i = 0;
    while (i < population) {
        Ribosome ribo = new Ribosome(cell);
        cell.addRibosome(ribo);
        i = i + 1;
    }


}
