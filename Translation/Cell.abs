module Cell;
export Cell;
import * from Transcription;
import * from Mrna;
import * from Ribosome;
import * from Protein;
import * from Degradation;
import * from Logger;


interface Cell {
    Unit addMRNA(Mrna mrna);
    Unit addRibosome(Ribosome ribo);
    Protein newProtein();
    Unit addProtein(Protein prot);
    Mrna obtainMrna();

    Int getQuantProtein();
    Int getQuantMrna();
    Int getQuantRibosomes();
    Unit degradeProtein();
    Unit degradeMrna();
}

class Cell implements Cell {
    List<Mrna> mrnaStrands = list[];
    Set<Ribosome> ribosomes = set[];
    Set<Protein> proteins = set[];

    // Keep track of number of synthesized aa
    Int aminoacids = 0;

    Unit addMRNA(Mrna mrna) {
        mrnaStrands = appendright(mrnaStrands, mrna);
    }

    Unit addRibosome(Ribosome ribo) {
        ribosomes = insertElement(ribosomes, ribo);
    }

    Protein newProtein() {
        return new local Protein();
    }

    Unit addProtein(Protein prot) {
        Int l = prot.getLength();
        if (l > 0) {
            proteins = insertElement(proteins, prot);
            aminoacids = aminoacids + l;
            println("Add Protein l=" + toString(l) + "; aa total=" + toString(aminoacids));
        }
    }

    Int getQuantProtein() {
        return size(proteins);
    }
    Unit degradeProtein() {
        assert size(proteins) > 0;
        Protein p = take(proteins);
        proteins = remove(proteins, p);
    }
    Int getQuantMrna() {
        return length(mrnaStrands);
    }
    Unit degradeMrna() {
        assert length(mrnaStrands) > 0;
        mrnaStrands = tail(mrnaStrands);
    }

    Int getQuantRibosomes() {
        return size(ribosomes);
    }

    // Randomly return one of the mRNA strands for decoding
    Mrna obtainMrna() {
        await length(mrnaStrands) > 0;
        Int n = random(length(mrnaStrands));
        Mrna mrna = nth(mrnaStrands, n);
        //String s = await mrna!toString();
        //println("Cell: offering mRNA: " + s);
        return mrna;
    }
}


/* Main block */

{
    Cell cell = new local Cell();


    // Start mRNA transcription
    // 0 -> mRNA, rate
    Transcription ts1 = new TranscriptionURA3(cell, 3);
    Transcription ts2 = new TranscriptionADH1(cell, 2);
    Transcription ts3 = new TranscriptionRRP8(cell, 1);


    // Start degradation
    // Prot -> 0, rate
    // mRNA -> 0, rate
    Degradation pd = new ProteinDegradation(cell, 1);
    Degradation md = new MrnaDegradation(cell, 1);

    Logger log = new CellResourcesLogger(cell);

    // Initial populations
    // Translation of proteins in E. coli is carried out by ~10,000-100,000 ribosomes
    Int pop_rib = 1;

    /*
     * Populate cell with ribosomes
     */
    Int i = 0;
    while (i < pop_rib) {
        Ribosome ribo = new Ribosome(cell);
        cell.addRibosome(ribo);
        i = i + 1;
    }

}
