module Ribosome;
export Ribosome;
import * from Dna;
import * from Mrna;
import * from Cell;
import * from Protein;

interface Ribosome {
    Unit run();
    Unit stop();
    Cell getCell();
}


/* Model of a ribosome
 * synthesizing protein from mRNA
 */
class Ribosome([Final] Cell cell) implements Ribosome {

    /*
     * Constants
     */
    // Translation rate bases/second (constant)
    // 10-50 bases/s <= http://book.bionumbers.org/how-many-mrnas-are-in-a-cell/
    // 12-21 bases/s <= http://bionumbers.hms.harvard.edu/bionumber.aspx?&id=100059
    [Final] Pair<Int,Int> speed = Pair(10,10);

    /*
     * Variables
     */
    Bool run = True;
    Bool attached = False;
    Mrna mrna;
    Int position = -1; // The ribosome's current position on the mRNA strand
    String mrnaID = ""; // The mRNA's name (id)

    Cell getCell() { return cell; }


    Unit run() {
        while (run) {
            /* Try to obtain mRNA; if successful,
             *     attach to mRNA,
             *     scan 5'UTR for start codon
             *     decode mRNA (codon by codon),
             *     detach upon encountering stop codon
             */
            mrna = await cell!getMrna();
            attached = await mrna!attach(this);
            position = 0;
            if (attached) {
                mrnaID = await mrna!getID();
                Bool can = this.scan(mrna);
                if (can)
                    this.decode(mrna);
                else {
                    // can't advance due to secondary structure
                    await mrna!detach(this, position);
                    println("Ribosome detached at pos " + toString(position));
                }
            } else {
                //println("Ribosome failed to attach to mRNA");
            }
        }
        println("Ribosome retired");
    }

    Unit stop() {
        if (attached) {
            await mrna!detach(this, position);
            mrna = null;
        }
        run = False;
    }

    /* Scan 5'UTR, look for the start codon
     */
    Bool scan(Mrna mrna) {
        Bool start = False;
        Bool canProceed = True;
        while (canProceed && !start) {
            Nucleotide n = await mrna!readNucleotide(position);
            canProceed = await mrna!canAdvance(this, position);
            position = await mrna!advance(this, position);
            if (n == A) {
                n = await mrna!readNucleotide(position);
                canProceed = await mrna!canAdvance(this, position);
                position = await mrna!advance(this, position);
                if (n == U) {
                    n= await mrna!readNucleotide(position);
                    canProceed = await mrna!canAdvance(this, position);
                    position = await mrna!advance(this, position);
                    if (n == G) {
                        start = True;
                    }
                }
            }
        }
        // arbitrary time to scan the UTR: it has to be > 0, otherwise failure has no visible effect
        await duration(1, 1);
        return canProceed;
    }

    Unit decode(Mrna mrna) {
        //println("Ribosome " + toString(this) + " starts decoding mrna at pos " + toString(position));
        List<Aminoacid> aminoacids = list[];
        Bool stop = False;
        while(!stop) {
            // read codon
            //println("position: " + toString(position));
            List<Nucleotide> codon = this.readCodon(mrna);
            if (stop(codon)) /* STOP; add protein */ {
                stop = True;
                //println("Ribosome stops decoding mrna; at pos " + toString(position));
                await mrna!detach(this, position);
                //println("Stop decoding mrna");
                await cell!addProtein(Protein(mrnaID, aminoacids));
            } else /* DECODE */ {
                //print("Codon: " + toString(codon));
                Aminoacid aa = aminoacid(nth(codon, 0), nth(codon, 1), nth(codon, 2));
                //println("  --> Aminoacid: " + toString(aa));
                aminoacids = appendright(aminoacids, aa);
                Int rate = random(snd(speed)-fst(speed)+1) + fst(speed);
                await duration(1/rate, 1/rate);
                // Since not a stop codon, assume another codon is following
                position = await mrna!advance(this, position);

                Bool hasATP = await cell!consumeATP();
                if (! hasATP) { // cell is out of energy
                    stop = True;
                    await mrna!detach(this, position);
                }
            }
         }
    }

    List<Nucleotide> readCodon(Mrna mrna) {
        List<Nucleotide> codon = list[];
        // #1
        Nucleotide n = await mrna!readNucleotide(position);
        codon = appendright(codon, n);
        position = await mrna!advance(this, position);
        // #2
        n = await mrna!readNucleotide(position);
        codon = appendright(codon, n);
        position = await mrna!advance(this, position);
        // #3
        n = await mrna!readNucleotide(position);
        codon = appendright(codon, n);
        assert length(codon) == 3;
        return codon;
    }

}


// decode codon into specific amino acid
def Aminoacid aminoacid(Nucleotide n1, Nucleotide n2, Nucleotide n3) =
    case n1 {
        U => case n2 {
            U => case n3 {
                U => Phe;
                C => Phe;
                A => Leu;
                G => Leu;
            };
            C => Ser;
            A => case n3 {
                U => Tyr;
                C => Tyr;
            };
            G => case n3 {
                U => Cys;
                C => Cys;
                G => Trp;
            };
        };
        C => case n2 {
            U => Leu;
            C => Pro;
            A => case n3 {
                U => His;
                C => His;
                A => Gln;
                G => Gln;
            };
            G => Arg;
        };
        A => case n2 {
            U => case n3 {
                U => Ile;
                C => Ile;
                A => Ile;
                G => Met;
            };
            C => Thr;
            A => case n3 {
                U => Asn;
                C => Asn;
                A => Lys;
                G => Lys;
            };
            G => case n3 {
                U => Ser;
                C => Ser;
                A => Arg;
                G => Arg;
            };
        };
        G => case n2 {
            U => Val;
            C => Ala;
            A => case n3 {
                U => Asp;
                C => Asp;
                A => Glu;
                G => Glu;
            };
            G => Gly;
        };
    };
